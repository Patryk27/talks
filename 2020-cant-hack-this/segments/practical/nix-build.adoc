== nix-build

[.prompter]
--
While `nix-shell` takes a Nix expression and drops you to a shell with "results" of that expression in scope,
`nix-build` builds an expression and stores it into the central storage.

As before, it's a pretty vague description, so let's start with an example.
--

== nix-build

[.prompter]
--
You remember that `netris` game I've introduced before?
Let's prepare a derivation for it, step by step.
--

== nix-build

[.prompter]
--
We're starting with a typical Nix boilerplate - it brings the `nixpkgs` channel into the scope, so that we can refer to
the standard library (in this case we're gonna need `mkDerivation`).
--

[source,nix]
----
let
  pkgs = import <nixpkgs> {};

in
  pkgs.stdenv.mkDerivation {
    /* ... */
  }
----

== nix-build

[.prompter]
--
Each derivation must be named and versioned; by the way, the name's key is called `pname`, because there already exist
an automatically-generated key called `name` containing both `pname` and `version`.
--

[source,nix]
----
  /* ... */

  pkgs.stdenv.mkDerivation {
    pname = "netris";
    version = "0.52";

    /* ... */
  }
----

== nix-build

[.prompter]
--
Then, we've gotta specify where from the application should be downloaded; in this case we're using a helper function
called `fetchFromGithub`, but the source doesn't necessarily have to be a Git repository - it might be an arbitrary
public or private, remote or local resource.

We've gotta specify the `sha256` up-front, because that's the way Nix ensures it's got the right file downloaded; when
hashes don't match, you get compile-time error. If this check didn't exist, you could - for instance - point `rev` at
`master`, which would break the build's reproducibility (that is: you could build the same Nix expression tomorrow and
get a different result, which is a big no-no).
--

[source,nix]
----
  /* ... */

  pkgs.stdenv.mkDerivation {
    /* ... */

    src = pkgs.fetchFromGitHub {
      owner = "naclander";
      repo = "netris";
      rev = "677...d2b"; # cut to fit
      sha256 = "0gm...9hm"; # cut to fit
    };

    /* ... */
  }
----

== nix-build

[.prompter]
--
Then we specify the package's dependencies.

One thing I've left unspoken up to this point is that Nix, to ensure that famous build reproducibility, performs builds
in a sandbox (not always, but it's an important aspect anyway).

Turning a blind eye onto a few intricacies, Nix's build sandbox is a chroot without access to the internet - that's one
of the reasons why we have to specify the source in advance instead of having one, giant Bash script full of
`git clone`-s and `wget`-s.

That's also the very reason Nix expressions always have to specify _all_ the dependencies; even if you already had
`ncurses` installed on your machine, if you left `buildInputs` empty, the build wouldn't pick it up thanks to the
sandboxing.

Contrary to what some people might think, I consider that a huge advantage - thanks to the sandboxing I can send you a
more-or-less arbitrary Nix expression and you can just build it, without having to ask me `eyy, do I need to install xyz
before?`, because almost always the answer's `no`: the expression is sufficient.
--

[source,nix]
----
  /* ... */

  pkgs.stdenv.mkDerivation {
    /* ... */

    buildInputs = [ pkgs.ncurses ];

    /* ... */
  }
----

== nix-build

[.prompter]
--
`netris` is a C game utilizing a traditional `Configure` & `Makefile` scripts, so we're lucky, because the
`mkDerivation` function is tailored for building this types of applications.

The only non-obvious thing is that I had to specify `dontAddPrefix`, because it just so happens that netris's
`Configure` script doesn't support that option.
--

[source,nix]
----
  /* ... */

  pkgs.stdenv.mkDerivation {
    /* ... */

    configureScript = "./Configure";
    dontAddPrefix = true;

    /* ... */
  }
----

== nix-build

[.prompter]
--
The `mkDerivation` function already provides a default `buildPhase` that automatically invokes `make`, so at this point
the last thing we gotta do is to copy the artifacts from build directory into the Nix store.

Since this is a turbo-basic game, we have only one artifact: the binary itself.
--

[source,nix]
----
  /* ... */

  pkgs.stdenv.mkDerivation {
    /* ... */

    installPhase = ''
      mkdir -p $out/bin
      cp ./netris $out/bin
    '';
  }
----

[.compact]
== nix-build

[.prompter]
--
Feast your eyes, enjoy the view - you could go ahead, save this into a file named `default.nix`, run `nix-build` and
in a minute or so we could be both playing `netris` together.
--

`default.nix`:

[source,nix]
----
let
  pkgs = import <nixpkgs> {};

in
  pkgs.stdenv.mkDerivation {
    pname = "netris";
    version = "0.52";

    src = pkgs.fetchFromGitHub {
      owner = "naclander";
      repo = "netris";
      rev = "6773c9b2d39a70481a5d6eb5368e9ced6229ad2b";
      sha256 = "0gmxbpn50pnffidwjchkzph9rh2jm4wfq7hj8msp5vhdq5h0z9hm";
    };

    buildInputs = [ pkgs.ncurses ];

    configureScript = "./Configure";
    dontAddPrefix = true;

    installPhase = ''
      mkdir -p $out/bin
      cp ./netris $out/bin
    '';
  }
----

[.compact]
== nix-build

[.prompter]
--
You don't have to confine yourself into `nix-build`-ing only binaries though - sky's the limit.

For instance, I'm using `nix-build` to compile my static blog - I'm using Asciidoctor, Hugo and Sass, and I've got
everything meticulously arranged inside a `default.nix` file that I'm running both locally and on my GitLab pipelines.

Basically, when I run `nix-build`, I end up with a bunch of HTML and CSS files.
--

[source,nix]
----
let
  pkgs = import <nixpkgs> {};
  deps = import ./deps.nix;

in
  pkgs.stdenv.mkDerivation {
    name = "stdout";
    src = ./src;

    buildInputs = with deps; [ asciidoctor hugo rsync sass ];

    phases = [ "buildPhase" ];

    buildPhase = ''
      /* ... */

      hugo -s "$out/src" --gc --minify

      /* ... */
    '';
  }
----

[.compact]
== nix-build

[.prompter]
--
As the last example: recently I've found myself OCR-ing lots of PDFs - to make my life easier, I've created a Nix
expression that, when run, launches `ocrmypdf` on all PDFs in current directory.

In this case, when I run `nix-build`, I end up with a bunch of OCR-ed PDFs.

Side note: obviously, you could just create a pure Bash script, but it has two disadvantages:

- first: you would have to keep `ocrmypdf` installed globally, which I'm not fond of,

- and second: Nix has a feature called `remote builds`, which automatically offloads all invocations of `nix-build` onto
  a remote host (taking care of uploading & downloading all the artifacts too, so remote builds are just as seamless as
  local ones).
  Essentially, when I run `nix-build` on my laptop, all the source files are copied onto a server, then the build
  happens _on the server_, and then Nix downloads just the resulting artifacts. Doing it manually via some fancy Bash is
  almost impossible for a generic case. I didn't mention this `remote builds` feature before, because I don't think that
  many people will find it useful, but - for me - it's a life-changer, especially while I'm running in a battery-saving
  mode.
--

[source,nix]
----
let
  pkgs = import <nixpkgs> {};

in
  pkgs.stdenv.mkDerivation {
    name = "pdfs";
    src = ./.;

    phases = [ "buildPhase" ];

    buildInputs = with pkgs; [ ocrmypdf ];

    buildPhase = ''
      mkdir $out

      for input in $src/*.pdf; do
        name=$(basename "$input")
        output="$out/$name"

        echo $input
        echo $output

        ocrmypdf -l pol --force-ocr "$input" "$output"
      done
    '';
  }
----

== nix-build: Extra stuff

- nix-bundle
- nix-copy-closure
- https://github.com/nix-community/nixpkgs-fmt

